#include "imports/stdlib.fc";
#include "types.func";
#include "op-codes.func";
#include "errors.func";
#include "messages.func";
#include "order_helpers.func";

int validate_dictionary_sequence(cell dict) impure inline {
    int index = -1;
    int expected_index = 0;
    do {
        (index, slice value, int found?) = dict.udict_get_next?(INDEX_SIZE, index);
        if (found?) {
            throw_unless(error::invalid_dictionary_sequence, index == expected_index);
            expected_index += 1;
        }
    } until (~ found?);
    return expected_index;
}

((int, cell, int, cell, int), ()) ~execute_order((int, cell, int, cell, int) storage, cell order_body) impure inline {

    accept_message();

    (int threshold, cell signers, int signers_num, cell proposers, int timelock_delay_seconds) = storage;

    int action_index = -1;
    do {
        (action_index, slice action, int found?) = order_body.udict_get_next?(ACTION_INDEX_SIZE, action_index);
        if (found?) {
            action = action.preload_ref().begin_parse();
            int action_op = action~load_op();
            if (action_op == actions::send_message) {
                int mode = action~load_uint(8);
                send_raw_message(action~load_ref(), mode);
                action.end_parse();
            } elseif (action_op == actions::update_multisig_params) {
                threshold = action~load_index();
                timelock_delay_seconds = action~load_uint(TIMELOCK_DELAY_SIZE);
                signers = action~load_nonempty_dict();
                signers_num = validate_dictionary_sequence(signers);
                throw_unless(error::invalid_signers, signers_num >= 1);
                throw_unless(error::invalid_threshold, threshold > 0);
                throw_unless(error::invalid_threshold, threshold <= signers_num);

                proposers = action~load_dict();
                validate_dictionary_sequence(proposers);

                action.end_parse();
            }
        }
    } until (~ found?);

    return ((threshold, signers, signers_num, proposers, timelock_delay_seconds), ());
}


(int, int, cell, int, cell, int, int) load_data() inline {
    slice ds = get_data().begin_parse();
    var data = (
        ds~load_order_seqno(), ;; next_order_seqno
        ds~load_index(), ;; threshold
        ds~load_nonempty_dict(), ;; signers
        ds~load_index(), ;; signers_num
        ds~load_dict(), ;; proposers
        ds~load_bool(),  ;; allow_arbitrary_order_seqno
        ds~load_uint(TIMELOCK_DELAY_SIZE) ;; timelock_delay_seconds
    );
    ds.end_parse();
    return data;
}

() save_data(int next_order_seqno, int threshold, cell signers, int signers_num, cell proposers, int allow_arbitrary_order_seqno, int timelock_delay_seconds) impure inline {
    set_data(
        begin_cell()
        .store_order_seqno(next_order_seqno)
        .store_index(threshold)
        .store_nonempty_dict(signers)
        .store_index(signers_num)
        .store_dict(proposers)
        .store_bool(allow_arbitrary_order_seqno)
        .store_uint(timelock_delay_seconds, TIMELOCK_DELAY_SIZE)
        .end_cell()
    );
}

() recv_internal(int balance, int msg_value, cell in_msg_full, slice in_msg_body) {
    slice in_msg_full_slice = in_msg_full.begin_parse();
    int msg_flags = in_msg_full_slice~load_msg_flags();
    if (msg_flags & 1) { ;; is bounced
        return ();
    }
    slice sender_address = in_msg_full_slice~load_msg_addr();

    if (in_msg_body.slice_bits() == 0) {
        return (); ;; empty message - just accept TONs
    }

    int op = in_msg_body~load_op();

    if (op == 0) {
        return (); ;; simple text message - just accept TONs
    }

    int query_id = in_msg_body~load_query_id();

    (int next_order_seqno, int threshold, cell signers, int signers_num, cell proposers, int allow_arbitrary_order_seqno, int timelock_delay_seconds) = load_data();

    if (op == op::new_order) {
        int order_seqno = in_msg_body~load_order_seqno();
        if (~ allow_arbitrary_order_seqno) {
            if (order_seqno == MAX_ORDER_SEQNO) {
                order_seqno = next_order_seqno;
            } else {
                throw_unless(error::invalid_new_order, (order_seqno == next_order_seqno));
            }
            next_order_seqno += 1;
        }

        int signer? = in_msg_body~load_bool();
        int index = in_msg_body~load_index();
        int expiration_date = in_msg_body~load_timestamp();
        int current_timestamp = now();
        cell order_body = in_msg_body~load_ref();
        in_msg_body.end_parse();
        (slice expected_address, int found?) = (signer? ? signers : proposers).udict_get?(INDEX_SIZE, index);
        throw_unless(error::unauthorized_new_order, found?);
        throw_unless(error::unauthorized_new_order, equal_slices_bits(sender_address, expected_address));
        throw_unless(error::expired, expiration_date >= current_timestamp);

        int order_unlock_date = current_timestamp + timelock_delay_seconds;
        throw_unless(error::expiration_date_less_than_unlock_date, expiration_date > order_unlock_date);

        int minimal_value = calculate_order_processing_cost(order_body, signers, expiration_date - current_timestamp);
        throw_unless(error::not_enough_ton, msg_value >= minimal_value);

        cell state_init = calculate_order_state_init(my_address(), order_seqno);
        slice order_address = calculate_address_by_state_init(BASECHAIN, state_init);
        builder init_body = begin_cell()
        .store_op_and_query_id(op::init, query_id)
        .store_index(threshold)
        .store_nonempty_dict(signers)
        .store_timestamp(order_unlock_date)
        .store_timestamp(expiration_date)
        .store_ref(order_body);

        send_message_with_state_init_and_body(
            order_address,
            0,
            state_init,
            init_body,
            BOUNCEABLE,
            SEND_MODE_CARRY_ALL_REMAINING_MESSAGE_VALUE | SEND_MODE_BOUNCE_ON_ACTION_FAIL
        );

    } elseif (op == op::execute) {
        ;; check that sender is order smart-contract and check that it has recent
        ;; signers dict

        int order_seqno = in_msg_body~load_order_seqno();
        int order_unlock_date = in_msg_body~load_timestamp();
        int expiration_date = in_msg_body~load_timestamp();
        int is_cancelled? = in_msg_body~load_bool();
        int approvals_num = in_msg_body~load_index();
        int signers_hash = in_msg_body~load_hash();
        cell order_body = in_msg_body~load_ref();
        in_msg_body.end_parse();

        cell state_init = calculate_order_state_init(my_address(), order_seqno);
        slice order_address = calculate_address_by_state_init(BASECHAIN, state_init);

        throw_unless(error::unauthorized_execute, equal_slices_bits(sender_address, order_address));
        throw_unless(error::singers_outdated, (signers_hash == signers.cell_hash()) & (approvals_num >= threshold));

        int current_timestamp = now();
        throw_unless(error::expired, expiration_date >= current_timestamp);
        throw_if(error::cancelled, is_cancelled?);
        throw_unless(error::timelock_not_expired, current_timestamp >= order_unlock_date);

        (threshold, signers, signers_num, proposers, timelock_delay_seconds)~execute_order(order_body);
    } elseif (op == op::execute_internal) {
        ;; we always trust ourselves, this feature is used to make chains of executions
        ;; where last action of previous execution triggers new one.

        throw_unless(error::unauthorized_execute, equal_slices_bits(sender_address, my_address()));
        cell order_body = in_msg_body~load_ref();
        in_msg_body.end_parse();
        (threshold, signers, signers_num, proposers, timelock_delay_seconds)~execute_order(order_body);
    }

    save_data(next_order_seqno, threshold, signers, signers_num, proposers, allow_arbitrary_order_seqno, timelock_delay_seconds);
}

(int, int, cell, cell, int) get_multisig_data() method_id {
    (int next_order_seqno, int threshold, cell signers, int signers_num, cell proposers, int allow_arbitrary_order_seqno, int timelock_delay_seconds) = load_data();
    throw_unless(error::inconsistent_data, signers_num == validate_dictionary_sequence(signers));
    validate_dictionary_sequence(proposers);
    throw_unless(error::invalid_signers, signers_num >= 1);
    throw_unless(error::invalid_threshold, threshold > 0);
    throw_unless(error::invalid_threshold, threshold <= signers_num);
    return (allow_arbitrary_order_seqno ? -1 : next_order_seqno, threshold, signers, proposers, timelock_delay_seconds);
}

int get_order_estimate(cell order, int expiration_date) method_id {
    (_, _, cell signers, _, _, _, _) = load_data();
    return calculate_order_processing_cost(order, signers, expiration_date - now());
}

slice get_order_address(int order_seqno) method_id {
    cell state_init = calculate_order_state_init(my_address(), order_seqno);
    return calculate_address_by_state_init(BASECHAIN, state_init);
}
